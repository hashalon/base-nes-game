/**
 * Handle drawing to the screen
 */

import "src/nes";
import "src/define";

namespace video {

    in zeropage {

        // specify if the sprites should be refreshed
        var draw_request : u8;

        // count the number of line crossed in the scanline interrupt
        var irq_counter : u8;

        // current scroll
        var scroll : u8;

        // used to read a sequence of numbers and write them into the PPU buffer
        var data_ptr  : *u8;
        var data_size :  u8;
    }

    // bit map for drawing
    let REQUEST_WAIT_FRAME     = 0b01;
    let REQUEST_UPDATE_SPRITES = 0b10;


    in program {

        // interrupt called at the end of every frame
        #[nmi] func vblank () {
            save_registers();
            ++scroll;
            set_scroll(scroll, 0);
            submit_oam_to_ppu();
            irq_counter = a = 0;
            load_registers();
        }

        // interrupt called at every scanline
        #[irq] func scanline () {
            save_registers();
            ++irq_counter;
            load_registers();
        }
    }


    // wait for PPU (2 vertical blanks)
    inline func wait_ppu_init () {
        for x in 0 .. 1 {
            do {} while !nes.ppu.status$7; // detect vertical blank
        }
    }

    // wait for draw request
    inline func wait_request () {
        draw_request = a = 0
            | REQUEST_WAIT_FRAME 
            | REQUEST_UPDATE_SPRITES;
        do {a = draw_request;} while !zero;
    }

    // write the appropriate address in the PPU
    inline func set_ppu_address (let address : u16) {
        a = nes.ppu.status;
        nes.ppu.address = a = >:address;
        nes.ppu.address = a = <:address;
    }

    // fill the ppu with the given data
    inline func set_ppu_data (val : u8 in y, len : u8 in x) {
        do {
            nes.ppu.data = val;
            --len;
        } while !zero;
    }
    inline func set_ppu_data_array (ptr : *u8 in p0, len : u8 in b2) {
        y = 0;
        do {
            nes.ppu.data = a = ptr[y++];
        } while y < len;
    }

    // write a single value in the PPU buffer
    inline func write_value_in_ppu (val : u8 in x, size : u8 in y) {
        do {
            nes.ppu.data = val;
            --size;
        } while !zero;
    }

    // write an array in the PPU buffer
    inline func write_array_in_ppu (let ptr : *u8, let len : u8) {
        y = 0;
        do {
            nes.ppu.data = a = ptr[y++];
        } while y < len;
    }

    // set the PPU scroll
    inline func reset_scroll () {
        a = nes.ppu.status; // reset the PPU high/low latch
        nes.ppu.scroll = a = 0;
        nes.ppu.scroll = a;
    }
    inline func set_scroll (sx : u8 in x, sy : u8 in y) {
        a = nes.ppu.status; // reset the PPU high/low latch
        nes.ppu.scroll = sx;
        nes.ppu.scroll = sy;
    }

    // push OAM buffer data to the ppu
    inline func submit_oam_to_ppu () {
        if {a = draw_request & REQUEST_UPDATE_SPRITES;} && !zero {
            nes.ppu.oam.address = a = <:&oam_buffer;
            nes.ppu.oam.dma     = a = >:&oam_buffer;
        }
        draw_request = a = OFF;
    }

    // setup IRQ call for per-scanline scrolling
    inline func setup_irq (let wait_scanlines : u8) {
        nes.mmc3.irq.disable = a = OFF; // acknowledge pending interrupts
        nes.mmc3.irq.latch   = a = wait_scanlines;
        nes.mmc3.irq.reload  = a;
        nes.mmc3.irq.disable = a = OFF; // latch in the countdown value
        nes.mmc3.irq.enable  = a;       // enable the IRQ counter
    }

}