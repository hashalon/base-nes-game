
import "src/nes";
import "src/define";

namespace joypad {

	// state of a joypad
	struct Joypad {
		current_state : u8,
		just_pressed  : u8,
		just_released : u8
	}

	in ram {
		var pad1, pad2 : Joypad;
	}

	// pool the state of both joypads
	inline func tick () {

		// store previous state
		var prev1 @ &b3 : u8;
		var prev2 @ &b4 : u8;
		prev1 = a = pad1.current_state;
		prev2 = a = pad2.current_state;

		// pool each button input of each joypad
		nes.joy.output = a = 1;
		nes.joy.output = a = 0;
		
		// store current state
		var curr1 @ &b1 : u8;
		var curr2 @ &b2 : u8;
		curr1 = a = pool(0); pad1.current_state = a; // store new current state
		curr2 = a = pool(1); pad2.current_state = a; // store new current state

		// deduce the button that have been just pressed or just released
		pad1.just_pressed  = a = ~prev1 & curr1;
		pad2.just_pressed  = a = ~prev2 & curr2;
		pad1.just_released = a = ~curr1 & prev1;
		pad2.just_released = a = ~curr2 & prev2;
	}

	in program {

		// read a joypad
		func pool (port : u8 in y) : u8 in a {

			// store the result to return
			var result @ &b0 : u8;

			// no need to reset 'result' because its 
			// bits will be completely erased anyway

			// count down to 0
			for x in nes.joy.bit.COUNT .. 1 by -1 {

				// LSR: push bit 0 of input into the carry
				a = (&nes.joy.input1)[port] >>> 1;

				// ROR: pull carry into bit 7 of target
				result >>>>#= 1;
			}
			return result;
		}

		// get horizontal direction for a joypad
		func to_horizontal (joy : u8 in b0) : u8 in x {
			x = 0;
			if joy$nes.joy.bit.LEFT  {--x;}
			if joy$nes.joy.bit.RIGHT {++x;}
			return x;
		}

		// get vertical direction for a joypad
		func to_vertical (joy : u8 in b0) : u8 in y {
			y = 0;
			// BIT '$' only support bits 7 and 6
			// in that case, we have to use regular bitmasking
			if {a = joy & nes.joy.mask.UP  ;} && !zero {--y;}
			if {a = joy & nes.joy.mask.DOWN;} && !zero {++y;}
			return y;
		}
	}

}